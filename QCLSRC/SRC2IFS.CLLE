PGM (&FILELIB &MBR &IFSPATH)

  DCL &FILELIB  *CHAR 20
  DCL &LIB      *CHAR 10
  DCL &FILE     *CHAR 10
  DCL &MBR      *CHAR 10
  DCL &IFSPATH  *CHAR 150
  DCL &IFSEXTN  *CHAR 10
  DCL &FULLPATH *CHAR 150
  DCL &FROMMBR  *CHAR 150
  DCL &USERID   *CHAR 10
  DCL &TEXT     *CHAR 50

  CHGVAR &FILE %SST(&FILELIB 1 10)
  CHGVAR &LIB  %SST(&FILELIB 11 10)
  RTVJOBA USER(&USERID)

  /* GET DEFAULT ROOT PATH IF A SPECIAL VALUE IS USED */
  IF (&IFSPATH = '*DFT') (DO)
     CHGVAR &IFSPATH ('/HOME/' *TCAT &USERID)
     MD &IFSPATH
     MONMSG CPF0000
  ENDDO
  IF (&IFSPATH = '*DFTLIB') (DO)
     CHGVAR &IFSPATH ('/HOME/' *TCAT &USERID)
     MD &IFSPATH
     MONMSG CPF0000
     CHGVAR &IFSPATH ('/HOME/' *TCAT &USERID *TCAT '/' *TCAT &LIB)
     MD &IFSPATH
     MONMSG CPF0000
  ENDDO

  /* GET SOURCE TYPE FOR IFS FILE EXTENSION AND THE SOURCE TEXT */
  RTVMBRD FILE(&LIB/&FILE) MBR(&MBR) SRCTYPE(&IFSEXTN) TEXT(&TEXT)

  /* CONVERT THE FILE EXTENSION AND NAME TO LOWERCASE */
  CALL SRC2IFS2 &IFSEXTN
  CALL SRC2IFS2 &FILE
  CALL SRC2IFS2 &MBR

  /* BUILD FOLDER PART OF PATH AND CREATE THE DIRECTORY IF NEEDED */
  CHGVAR &FULLPATH (&IFSPATH *TCAT '/' *TCAT &FILE)
  MD &FULLPATH
  MONMSG CPF0000

  /* ADD THE FILE NAME AND EXTNESION TO THE PATH */
  CHGVAR &FULLPATH (&FULLPATH *TCAT '/' *TCAT &MBR *TCAT '.' *TCAT &IFSEXTN)

  /* BUILD THE FULL PATH FOR THE FROM MEMEBR */
  CHGVAR &FROMMBR ('/QSYS.lib/' *TCAT &LIB *TCAT '.lib/' *TCAT &FILE *TCAT +
                   '.file/' *TCAT &mbr *tcat '.mbr')

  CPYTOSTMF FROMMBR(&FROMMBR) TOSTMF(&FULLPATH) +
            STMFOPT(*REPLACE) STMFCCSID(1252) DBFCCSID(37)

  /* ADD THE SOURCE TEXT TO THE FILE ATRIBUTE TEXT */
  CHGATR OBJ(&FULLPATH) ATR(*TEXT) VALUE(*NONE) TEXT(&TEXT)

ENDPGM
